{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";

() recv_internal() {
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  if cell_hash(tree) == hash { return tree; }
  cell c = tree;
  slice s = c.begin_parse();
  tuple stack1 = null();
  tuple stack2 = null();
  repeat (s.slice_refs()) {
    stack1~push_back(s~load_ref());
  }
  while (~ stack1.is_null()) | (~ stack2.is_null()) {
    while (~ stack1.is_null()) {
        c = stack1~pop_back();
        if cell_hash(c) == hash { return c; }
        s = c.begin_parse();
        repeat (s.slice_refs()) { stack2~push_back(s~load_ref()); }
    }
    while (~ stack2.is_null()) {
        c = stack2~pop_back();
        if cell_hash(c) == hash { return c; }
        s = c.begin_parse();
        repeat (s.slice_refs()) { stack1~push_back(s~load_ref()); }
    }
  }
  return begin_cell().end_cell();
}
